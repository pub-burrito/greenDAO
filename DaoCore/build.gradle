apply plugin: 'java'
apply plugin: 'maven'

group = 'de.greenrobot'
version = '1.3.7-java'
sourceCompatibility = 1.6

def isSnapshot = version.endsWith('-SNAPSHOT')
def preferedRepo
def preferedRepoId
def parsedProjectXml = (new XmlParser()).parse("$System.env.HOME" + "/.m2/settings.xml")
def username
def password

def passwordSnapshots = parsedProjectXml.servers.server[1].password.text()
if(isSnapshot) {
  preferedRepo = "http://archiva.cliff.cloudburrito.com:8080/archiva/repository/snapshots/"
  preferedRepoId = "cloud.archiva.snapshots"
  username = parsedProjectXml.servers.server[0].username.text()
  password = parsedProjectXml.servers.server[0].password.text()

} else {
  preferedRepo = "http://archiva.cliff.cloudburrito.com:8080/archiva/repository/internal/"
  preferedRepoId = "cloud.archiva.internal"
  username = parsedProjectXml.servers.server[1].username.text()
  password = parsedProjectXml.servers.server[1].password.text()
}

repositories {
    mavenCentral()
}



// provided is not supported in Gradle 1.4, see http://issues.gradle.org/browse/GRADLE-784
// Like this, it won't appear at all in the POM
configurations {
    provided
    deployerJars
}

dependencies {
    provided 'com.google.android:android:4.1.1.4'
    provided 'com.google.android:android-test:4.1.1.4'
    provided 'com.google.android:annotations:4.1.1.4'
    provided 'com.google.android:support-v4:r7'
    // deployerJars 'org.apache.maven.wagon:wagon-webdav-jackrabbit:2.4'
    deployerJars 'org.apache.maven.wagon:wagon-webdav:1.0-beta-2'
}

sourceSets {
    main {
        compileClasspath += configurations.provided
        java {
            srcDir 'src'
        }
    }
}

javadoc {
    classpath += configurations.provided
    title = " greenDAO ${version} API"
	options.bottom = 'Available under the Apache License, Version 2.0 - <i>Copyright &#169; 2011-2013 <a href="http://greenrobot.de/">greenrobot.de</a>. All Rights Reserved.</i>'
	excludes = ['de/greenrobot/dao/internal','de/greenrobot/dao/Internal*']
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: preferedRepo, id: preferedRepoId) {
              authentication(userName: username, password: password)
            }
            pom.groupId = "${group}"
            pom.artifactId = "${rootProject.name}"
            pom.version = "${version}"
            pom.project {
               name 'greenDAO'
               packaging 'jar'
               description 'greenDAO is a light and fast ORM for Android'
               url 'http://greendao-orm.com'

               scm {
                   url 'https://github.com/greenrobot/greenDAO'
                   connection 'scm:git@github.com:greenrobot/greenDAO.git'
                   developerConnection 'scm:git@github.com:greenrobot/greenDAO.git'
               }

               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'greenrobot'
                       name 'greenrobot'
                   }
               }
               
               issueManagement {
                   system 'GitHub Issues'
                   url 'https://github.com/greenrobot/greenDAO/issues'
               }
               
               organization {
                   name 'greenrobot'
                   url 'http://greenrobot.de'
               }
           }
        }
    }
}